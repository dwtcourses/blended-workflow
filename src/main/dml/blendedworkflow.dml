package pt.ist.socialsoftware.blendedworkflow.engines.domain;

enum pt.ist.socialsoftware.blendedworkflow.engines.domain.DataModel.DataState as DataState;
enum pt.ist.socialsoftware.blendedworkflow.engines.domain.Goal.GoalState as GoalState;
enum pt.ist.socialsoftware.blendedworkflow.engines.domain.Task.TaskState as TaskState;
enum pt.ist.socialsoftware.blendedworkflow.engines.domain.WorkItem.WorkItemState as WorkItemState;
enum pt.ist.socialsoftware.blendedworkflow.engines.domain.Attribute.AttributeType as AttributeType;
enum pt.ist.socialsoftware.blendedworkflow.engines.domain.Relation.Cardinality as Cardinality;

class BlendedWorkflow {
	Integer bwSpecificationCounter;
}

class BWSpecification {
	String name;
	String yawlSpecficationID;
	Integer bwInstanceCounter;
}

class BWInstance {
	String ID;
	String yawlCaseID;
	Integer workItemCounter;
}

class WorkItem {
	String ID;
	WorkItemState state;
}

class TaskWorkItem extends WorkItem;

class GoalWorkItem extends WorkItem;

class GoalModel;

class GoalModelInstance extends GoalModel;

class Goal {
	String name;
	GoalState state;
}

class TaskModel;

class TaskModelInstance extends TaskModel;

class Task {
	String name;
	TaskState state;
}

class Condition;

class AndCondition extends Condition;

class OrCondition extends Condition;

class NotCondition extends Condition;

class ExistsAttributeCondition extends Condition;

class ExistsEntityCondition extends Condition;

class CompareAttributeToValueCondition extends Condition {
	String operator;
	String value;
}

class TrueCondition extends Condition;

class DataModel;

class DataModelInstance extends DataModel { 
}

class Entity {
	String name;
	Integer entityInstanceCounter;
}

class EntityInstance {
	String ID;
	Integer attributeInstanceCounter;
	Integer relationInstanceCounter;
}

class Attribute {
	String name;
	AttributeType type;
	boolean isKeyAttribute;
}

class AttributeInstance {
	String ID;
	String value;
	DataState state;
}

class Relation {
	String name;
	Cardinality cardinalityOne;
	Cardinality cardinalityTwo;
	boolean isOneKeyEntity;
	boolean isTwoKeyEntity;
}

class RelationInstance {
	String ID;
}

// BlendedWorkflow
relation BlendedWorkflowHasBWSpecifications {
	BlendedWorkflow playsRole blendedWorkflow {multiplicity 0..1;}
	BWSpecification playsRole bwSpecifications {multiplicity 0..*;}
}

// BWSpecification
relation BWSpecificationHasBWInstances {
	BWSpecification playsRole bwSpecification {multiplicity 0..1;}
	BWInstance playsRole bwInstances {multiplicity 0..*;}
}

relation BWSpecificationHasGoalModel {
	BWSpecification playsRole bwSpecification {multiplicity 0..1;}
	GoalModel playsRole goalModel { multiplicity 0..1; }
}

relation BWSpecificationHasTaskModel {
	BWSpecification playsRole bwSpecification {multiplicity 0..1;}
	TaskModel playsRole taskModel { multiplicity 0..1; }
}

relation BWSpecificationHasDataModel {
	BWSpecification playsRole bwSpecification {multiplicity 0..1;}
	DataModel playsRole dataModel { multiplicity 0..1; }
}

// BWInstance
relation BWInstanceHasGoalModelInstance {
	BWInstance playsRole bwInstance {multiplicity 0..1;}
	GoalModelInstance playsRole goalModelInstance {multiplicity 0..1;}
}

relation BWInstanceHasTaskModelInstance {
	BWInstance playsRole bwInstance {multiplicity 0..1;}
	TaskModelInstance playsRole taskModelInstance {multiplicity 0..1;}
}

relation BWInstanceHasDataModelInstance {
	BWInstance playsRole bwInstance {multiplicity 0..1;}
	DataModelInstance playsRole dataModelInstance {multiplicity 0..1;}
}

relation BWInstanceHasWorkItems {
	BWInstance playsRole bwInstance {multiplicity 0..1;}
	WorkItem playsRole workItems {multiplicity 0..*;}
}

// WorkItem
relation WorkItemRefersToAttributeInstances {
	WorkItem playsRole workItems {multiplicity 0..*;}
	AttributeInstance playsRole contraintViolationAttributeInstances { multiplicity 0..*; }
}

relation TaskWorkItemHasAttributteInstances {
	TaskWorkItem playsRole taskWorkItems {multiplicity 0..*;}
	AttributeInstance playsRole preConstraintAttributeInstances { multiplicity 0..*; }
}

// Goal Model
relation GoalModelHasGoals {
	GoalModel playsRole goalModel { multiplicity 0..1; }
	Goal playsRole goals { multiplicity 0..*; }
}

// Goal
relation GoalHasGoals {
	Goal playsRole parentGoal { multiplicity 0..1; }
	Goal playsRole subGoals { multiplicity 0..*; }
}

relation GoalHasCondition {
	Goal playsRole goal { multiplicity 0..1; }
	Condition playsRole condition { multiplicity 0..1; }
}

relation GoalHasGoalWorkItem {
	Goal playsRole goal { multiplicity 0..1; }
	GoalWorkItem playsRole goalWorkItem { multiplicity 0..1; }
}

// Task Model
relation TaskModelHasGoals {
	TaskModel playsRole taskModel { multiplicity 0..1; }
	Task playsRole tasks { multiplicity 0..*; }
}

// Task
relation TaskHasPreCondition {
	Task playsRole taskPreCondition { multiplicity 0..1; }
	Condition playsRole preConstraint { multiplicity 0..1; }
}

relation TaskHasPostCondition {
	Task playsRole taskPosCondition { multiplicity 0..1; }
	Condition playsRole postConstraint { multiplicity 0..1; }
}

relation TaskHasGoalWorkItem {
	Task playsRole task { multiplicity 0..1; }
	TaskWorkItem playsRole taskWorkItem { multiplicity 0..1; }
}


// DataModel
relation DataModelHasEntities {
	DataModel playsRole dataModel { multiplicity 0..1; }
	Entity playsRole entities { multiplicity 0..*; }
}

relation DataModelHasAttributes {
	DataModel playsRole dataModel { multiplicity 0..1; }
	Attribute playsRole attributes { multiplicity 0..*; }
}

relation DataModelHasRelation {
	DataModel playsRole dataModel { multiplicity 0..1; }
	Relation playsRole relations { multiplicity 0..*; }
}

// Entity
relation EntityHasAttributes {
	Entity playsRole entity { multiplicity 0..1; }
	Attribute playsRole attributes { multiplicity 0..*; }
}

relation EntityMayHaveRelations {
	Entity playsRole entities { multiplicity 0..*; } //FIXME 0..2
	Relation playsRole relations { multiplicity 0..*; }
}

relation EntityHasEntityInstances {
	Entity playsRole entity { multiplicity 0..1; }
	EntityInstance playsRole entityInstances { multiplicity 0..*; }
}

// Relation
relation RelationHasRelationInstances {
	Relation playsRole relationType { multiplicity 0..1; } //FIXME relation
	RelationInstance playsRole relationInstances { multiplicity 0..*; }
}

// Attributes
relation AttributeHasAttributeInstances {
	Attribute playsRole attribute { multiplicity 0..1; }
	AttributeInstance playsRole attributeInstances { multiplicity 0..*; }
}

// EntityInstance
relation EntityInstanceHasAttributeInstances {
	EntityInstance playsRole entityInstance { multiplicity 0..1; }
	AttributeInstance playsRole attributeInstances { multiplicity 0..*; }
}

relation EntityInstanceMayHaveRelationInstances {
	EntityInstance playsRole entityInstances { multiplicity 0..*; } //FIXME 0..2
	RelationInstance playsRole relationInstances { multiplicity 0..*; }
}

// Conditions
// AndCondition
relation AndConditionHasConditions {
	AndCondition playsRole andCondition { multiplicity 0..1; }
	Condition playsRole conditions { multiplicity 0..*; } //FIXME 0..2
}

// OrCondition
relation OrConditionHasConditions {
	OrCondition playsRole orCondition { multiplicity 0..1; }
	Condition playsRole conditions { multiplicity 0..*; } //FIXME 0..2
}

// NotCondition
relation NotConditionHasConditions {
	NotCondition playsRole notCondition { multiplicity 0..1; }
	Condition playsRole condition { multiplicity 0..1; }
}

// ExistsAttributeCondition
relation ExistsAttributeConditionHasAttributes {
	ExistsAttributeCondition playsRole existsAttributeCondition { multiplicity 0..*; }
	Attribute playsRole attribute { multiplicity 0..1; }
}

//ExistsEntityCondition
relation ExistsEntityConditionHasEntities {
	ExistsEntityCondition playsRole existsEntityCondition { multiplicity 0..*; }
	Entity playsRole entity { multiplicity 0..1; }
}


// CompareAttributeToValueCondition
relation CompareAttributeToValueConditionHasAttributes {
	CompareAttributeToValueCondition playsRole compareAttributeToValueCondition { multiplicity 0..*; }
	Attribute playsRole attribute { multiplicity 0..1; }
}

